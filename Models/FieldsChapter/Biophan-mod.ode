# BMB_04.ode
#
# This XPPAUT file contains the program for pancreatic beta-cells, published
# by Bertram and Sherman, Bull. Math. Biol., 66:1313-1344, 2004.

# Variables:
#    v -- voltage
#    n -- activation variable for a delayed rectifier
#    c -- free cytosolic calcium concentration
#    cer -- concentration of free calcium in the endoplasmic reticulum
#    a -- fraction of activated KATP channels

v(0)=-60
n(0)=0
c(0)=0.1
a(0)=0.46
cer(0)=95

#par a=1

# Persistence of slow bursting in tolbutamide and Tg
# gkatpbar=300, gkca=100, r=0.04, kserca=0

# Conductances in pS; currents in fA; Ca concentrations in uM; time in ms

# conductances
# par gkatpbar=500
par gkatp0=500, gkatp1=300, gkatp2=100
gkatpbar = gkatp0 + heav(t - t1)*(gkatp1 - gkatp0) + heav(t - t2)*(gkatp2 - gkatp1)

# Ikca (800 = fast , 700 = medium, 100 = slow)
par gkca=200

# ADP parameters (no glucose, r=0, kserca=0.05
                  low glucose, r=0.04, kserca=0.1
#                 med. glucose, r=0.14, kserca=0.4)
# par r=0.14, kserca=0.4
par kserca=0.4
# par r0=0, r1=0.1, r2=0.16
par r0=0, r1=0.0, r2=0.0
par t1=800000, t2=160000
r = r0 + heav(t - t1)*(r1 - r0) + heav(t - t2)*(r2 - r1)
# Ca parameters  (sigmav=cyt volume/ER volume)
par sigmav=5, kc=0.2

# Ik
number vn=-16, vk=-75, taun=20, gk=3000, sn=5
# Ica
number vca=25, gca=1200
number vm=-20, sm=12
# Ikca
number kd=0.3
# Miscellaneous
lambda=1.25
number cm=5300
# Calcium Handling
number f=0.01, fer=0.01
number alpha=4.50e-6
# Calcium Handling: ER
par perl=0.0005
par sa=0.02, taua=30000

# Activation variables
ninf = 1/(1+exp((vn-v)/sn))
minf = 1/(1+exp((vm-v)/sm))
ainf = 1/(1+exp((r-c)/sa))
omega = 1/(1+(kd/c)^5)

# Ionic currents
ica = gca*minf*(v-vca)
ikca = gkca*omega*(v-vk)
ikatp = gkatpbar*a*(v-vk)
#ikatp=gkatpbar*(v-vk)
ik = gk*n*(v-vk)

% ER functions
jerp = kserca*c

% Ca fluxes
jmemtot = -(alpha*ica + kc*c)
jerleak = perl*(cer - c)
jertot = jerleak - jerp

# Differential Equations

v' = (-ica - ik - ikatp  - ikca)/cm
n' =  lambda*(ninf - n)/taun
c' = f*(jertot + jmemtot) 
a' = (ainf-a)/taua 
cer' = -fer*sigmav*jertot

@ meth=cvode, toler=1.0e-10, atoler=1.0e-10, dt=20.0, total=180000, 
@ maxstor=20000,bounds=10000000, xp=tsec, yp=v
@ xlo=0, xhi=180, ylo=-70, yhi=-10

aux tsec=t/1000
aux tmin=t/60000

done
