#basic model
#bursting with single slow variable

#units: V=mV; t=ms; g=pS; I=fA
#
#Reference:  Bertram and Sherman. 
#Calcium-based model for pancreatic islets
#Figure 1A...bursting generated by basic model
#
#Ica- calcium current
#Ik- delayed rectifier K+ current
#Ik(Ca)- Ca2+ dependent K+ current
#Ik(ATP)- nucleotide-sensitive K+ current
#c - cytosolic free Ca2+ concentration

#initial conditions
init v=-60, n=0.00014, c=0.1

#parameters
par gca=1200, gkca=300, gk=3000, gkatp=230
par vca=25, vk=-75, cm=5300
par taun=16, alpha=4.5e-6
par fcyt=0.01, kPMCA=0.2, kD=0.3
par vn=-16, vm=-20, sn=5, sm=12

# ionic currents
Ica(v)=gca*minf(v)*(v-vca)
Ik(v)=gk*n*(v-vk)
Ikca(v)=gkca*w*(v-vk)
Ikatp(v)=gkatp*(v-vk)

#activation functions
minf(v)=1.0/(1.0+exp((vm-v)/sm))
ninf(v)=1.0/(1.0+exp((vn-v)/sn))

#flux of Ca2+ through the membrane
Jmem=-(alpha*Ica(v)+kPMCA*c)

#differential equations
v'=-(Ica(v)+Ik(v)+Ikca(v)+Ikatp(v))/Cm
n'=(ninf(v)-n)/taun
c'=fcyt*Jmem

#fraction of K(Ca) channels activated by cytosolic Ca2+
w=c^5/(c^5+kD^5)

aux ex=gkca*w
aux omega=w
aux tsec=t/1000.0

#XPP parameters
@ meth=cvode, dtmax=1, dt=2.0, total=10000, maxstor=1000000
@ bounds=1000, xp=tsec, yp=v, toler=1.0e-9, atoler=1.0e-9
@ xlo=0, xhi=10, ylo=-70, yhi=-10

done
