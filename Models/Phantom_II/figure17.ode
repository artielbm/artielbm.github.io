#bursting with three slow variable

#units: V=mV; t=ms; g=pS; I=fA
#Reference:  Bertram and Sherman. 
#Calcium-based model for pancreatic islets
#Figure 17...slow bursting with application of tolbutamide (Tolb.) 
# followed by application of Tg

#Ica- calcium current
#Ik- delayed rectifier K+ current
#Ik(Ca)- Ca2+ dependent K+ current
#Ik(ATP)- nucleotide-sensitive K+ current
#c - cytosolic free Ca2+ concentration
#cer - ER Ca2+ concentration
#initial conditions
init v=-65.0, c=0.09, n=0.01, cer=25, a=0.8

#parameters
par gca=1200, gkca=100, gk=3000, gkatp=230
par vca=25, vk=-75, cm=5300
par taun=16, alpha=4.5e-6
par fcyt=0.01, kpmca=0.2, kd=0.3
par vn=-16, vm=-20, sn=5, sm=12
par dact=0.35, dinact=0.4
par fer=0.01, pleak=0.0005, dip3=0.5, vcytver=5
par ip3=0, ggkatp=300, sa=0.1, taua=300000
par tks=600000
par r=0.04

kserca=if(t<tks)then(0.1)else(0.0)

# ionic currents
ica(v)=gca*minf(v)*(v-vca)
ik(v)=gk*n*(v-vk)
ikca(v)=gkca*w*(v-vk)
ikatp(v)=ggkatp*a*(v-vk)

#activation functions
minf(v)=1.0/(1.0+exp((vm-v)/sm))
ninf(v)=1.0/(1.0+exp((vn-v)/sn))
ainf(c)=1.0/(1.0+exp((r-c)/sa))

#fraction of K(Ca) channels activated by cytosolic Ca2+
w=c^5/(c^5+kd^5)

#flux of Ca2+ through the membrane
jmem=-(alpha*Ica(v)+kpmca*c)

#Ca2+ influx into the ER via SERCA 
jserca=kserca*c

#efflux out of the ER has two components
# 1. Ca2+ leak is proportional to gradient between Ca2+ and ER
jleak=pleak*(cer-c)

# 2. Ca2+ efflux through the IP3R
jip3=oinf*(cer-c)

#fraction of open channels
oinf=(c/(dact+c))*(ip3/(dip3+ip3))*(dinact/(dinact+c))

#net Ca2+ efflux from the ER
jer=jleak+jip3-jserca

#differential equations
v'=-(ica(v)+ik(v)+ikca(v)+ikatp(v))/cm
n'=(ninf(v)-n)/taun
c'=fcyt*(jmem+jer)
cer'=-fer*(vcytver)*jer
a'=(ainf(c)-a)/taua

aux tmin=t/60000.0

@ meth=cvode, dtmax=1, dt=10, total=1.2e6, maxstor=1000000
@ bounds=1e10, xp=tmin,  yp=v, toler=1.0e-7, atoler=1.0e-7
@ xlo=0, xhi=20, ylo=-80, yhi=-20

done
