# Pituitary cell model using a common core of ionic currents
# Adding ER calcium, a cytosolic calcium subspace, and IP3 receptor dynamics 
#
# Figure 7 of "Common and diverse elements of ion channels and receptors underlying electrical activity in endocrine pituitary cells" by Fletcher, Sherman, and Stojilkovic. Molecular and Cellular Endocrinology, 2017
#
# left panel: sigmad=39, ip3p=0.22, ggqp=0.15
# right panel: sigmad=1, ip3p=0.5, ggqp=0.15

par sigmad=39
par ip3b=0

par ip3p=0
par ggqp=0

par gnav=20
par gbk=0
par gca=1.5
par gk=5
par gkca=2.5
par gnab=0.1
par gkir=0.9

par ggq=0
par ggirk=0
par ggirkp=0


num ek=-75
num eca=60
num ena=75
num ens=0

vkdrive=v-ek
vnadrive=v-ena
vcadrive=v-eca
vnsdrive=v-ens


###### cell geometry ############
num alpha=0.0015
num cm=6

par sigmaer=39

####### smooth pulse profiles ############
# first pulse is for IP3 level, second for conductance gGq
# time to peak is 2*tau, peak value is 1. 

par kp=0.04
par tau=17500
num tpulse=5000
delpulse=t-tpulse
pnorm=4*tau*tau*exp(-2)
pulse=delpulse*delpulse/pnorm*exp(-delpulse/tau)*heav(delpulse)
g=pulse/(pulse+kp) * (1+kp)

par kp2=100
par tau2=17500
num tpulse2=5000
delpulse2=t-tpulse2
pnorm2=4*tau2*tau2*exp(-2)
pulse2=delpulse2*delpulse2/pnorm2*exp(-delpulse2/tau2)*heav(delpulse2)
g2=pulse2/(pulse2+kp2) * (1+kp2)


###### sodium #########################

# ina -- ttx-sensitive
num tauhna=2
num vmna=-15
num smna=5
num vhna=-55
num shna=-10
mnainf = 1/(1+exp((vmna-v)/smna))
hnainf = 1/(1+exp((vhna-v)/shna))
inav = gnav*mnainf*mnainf*mnainf*hna*vnadrive


###### calcium ########################

# ica
num vmca=-15
num smca=12
mcainf = 1/(1+exp((vmca-v)/smca))
ical = gca*mcainf*vcadrive

ica = ical


###### potassium ######################

# ik
num vn=0
num sn=5
num taun=20
ninf = 1/(1+exp((vn-v)/sn))
ikdr = gk*n*vkdrive


# ikca -- sk, ik, and/or bk far from ca2+ channels
num kkca=0.4
c2=c*c
nkcainf=c2/(c2+kkca*kkca)
ikca = gkca*nkcainf*vkdrive


# ibk -- due to bk channels near ca2+ channels, following domain ca
num taunbk=5
num vbk0=0.1
num snbk=10
num a=0.11, kshift=18
num kcabk=1.5
cad = -a*ical 
vnbk   = vbk0-kshift*log(cad/kcabk) 
nbkinf = 1/(1+exp((vnbk-v)/snbk))
ibk = gbk*nbk*vkdrive


# ikir and GIRK
num vnkir=-55
num snkir=-10
nkirinf = 1/(1+exp((vnkir-v)/snkir))
ikir = gkir*nkirinf*vkdrive
igirk = (ggirk+ggirkp*g2)*nkirinf*vkdrive

ik = ikca + ibk + ikdr  + ikir + igirk 


######## non-specific #################

# inab
inab = gnab*vnsdrive

igq=(ggq+ggqp*g2)*vnsdrive

ins=inab +igq


###### calcium handling ###############

num fcyt=0.01
num fer=0.01

jin = -alpha*ica

# jout consists of a plasma membrane atpase and a small linear leak to prevent c->infinity in some circumstances
par nupmca=0.02
num kpmca=0.1
jpmca=nupmca*c2/(c2+kpmca*kpmca) 

par kpmlin=0.001
jout=jpmca + kpmlin*c

# ER refilling
par nuserca=0.16
num kserca=0.2
jref=nuserca*c2/(c2+kserca*kserca)

#jrel consists of ER leak and IP3 receptor
par pleak=0.0002
par pip3=20
par ki=1
par ka=.8
par kd=.8
par aip3=0.002

ip3=ip3b+ip3p*g

num=(ip3*cd*hip3r)
num3=num*num*num
denom=(ip3+ki)*(cd+ka)
denom3=denom*denom*denom

jip3r=pip3*num3/denom3*(cer - cd)
jleak=pleak*(cer - cd)
jrel = jleak+jip3r

#calcium subspace
par kdiff=.6
jdiff=kdiff*(cd-c)


par vclamp=0
par vhold=-65

##### Equations #######################
v'= -(inav+ica+ik+ins)/Cm*(1-vclamp)+vclamp*(vhold-v)

c' = fcyt*(jin-jout+jdiff-jref)
cer' = fer*sigmaER*(jref-jrel)
cd' = fcyt*sigmaD*(jrel-jdiff)

hna'= (hnainf-hna)/tauhna
n'= (ninf-n)/taun
nbk'= (nbkinf-nbk)/taunbk

hip3r' = Aip3*(Kd-(cd + Kd)*hip3r)


init v=-60.58

init c=0.0881
init cer=124.49
init cd=0.130

init hna=0.637
init n=0.0
init nbk=0.0

init hip3r=0.862

##### Auxiliary outputs ###############
aux tsec=t/1000
aux cdbk=cad

aux inav=inav
aux ical=ical
aux ikdr=ikdr
aux ikca=ikca
aux ibk=ibk
aux inab=inab
aux ikir=ikir

aux jin=jin
aux jout=jout
aux jip3r=jip3r
aux cdip3=cd

aux pulse=g

##### Options #########################
@ dt=1
@ total=60000

@ maxstor=200000, bounds=10000000, xp=t, yp=v
@ xlo=0, xhi=30000, ylo=-90, yhi=30, bell=0
@ ntst=150,nmax=2000,npr=2000,ds=0.01,parmin=-1,parmax=2
@ autoxmin=0, autoxmax=1,autoymin=-75,autoymax=25
done
