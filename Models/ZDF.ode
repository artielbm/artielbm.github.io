### Paper 1 clean-up ode for rodents, August 30, 2014


###### System parameters
par Eg0=1.44, k=700, R0_init=864, BV=0.01

 

###### Define metabolic rate M and Insulin secretion rate ISR
par Mmax=1, alpha_M=150, kM=2 
M = Mmax*G^kM/(alpha_M^kM + G^kM)

par alpha_ISR=1.2, kISR=2
ISR = sigma*(M+gamma)^kISR/(alpha_ISR^kISR + (M+gamma)^kISR)


###### Build b dynamics  and Define Proliferation rate P(ISR) and Apoptosis A(M)
par Pmax=4.55, kP=4, alpha_P=41.77
P_ISR=Pmax*ISR^kP/(alpha_P^kP + ISR^kP)

par Amax=3.11, alpha_A=0.44, kA=6, A_b=0.8
A_M=Amax*M^kA/(alpha_A^kA + M^kA) + A_b


P= P_ISR
A= A_M

par tau_b=42.85

###### Build gamma dynamics

par G_bar=0.2, Gs=99.9, Gn=1, Gshft=0.1
G_inf=G_bar/(1+ exp(-(G-Gs)/Gn)) - Gshft
gamma_inf=G_inf


par tau_g=2.14

###### Build sigma dynamics
# Define a sifted M (M_Gsh) with G and a shifted ISR with the shifted M (ISR_Gsh)
par sigma_Gsh=75
M_Gsh = Mmax*(G-sigma_Gsh)^kM/(alpha_M^kM + (G-sigma_Gsh)^kM)
ISR_Gsh = sigma*(M_Gsh+gamma)^kISR/(alpha_ISR^kISR + (M_Gsh+gamma)^kISR)

# Define a positive feedback from ISR (sigma_ISRI)
par  ISRI_bar=867.6,ISRI_s=0.1,ISRI_n=0.1, ISRI_k=1, sigma_b=3
sigma_ISRI=ISRI_bar/(1+ ISRI_k*exp(-(ISR_Gsh-ISRI_s)/ISRI_n))

# Define a negative feedback from M (sigma_MI)
par MI_bar=1, MI_k=0.2, MI_s=0.2, MI_n=0.02
sigma_MI= 1 - MI_bar/(1 + MI_k*exp(-(M_Gsh-MI_s)/MI_n))

sigma_inf=sigma_MI*sigma_ISRI + sigma_b 


par tau_sigma=10.71

##### Enforced Insulin resistance parameters (Si)
par t_si=10000, target_si=0.8


# Controling Si and R0 for simulating Intensive Life Style Intervention (ILI) and bariatric surgery 

par Si_tar=0.4,  R0_tar=864, sigma_tar=90
par tonsi=14000000, tonsigma=14000000,tonR0=14000000
global 1 t-tonsi {Si=Si_tar}
global 1 t-tonsigma {sigma=sigma_tar}
R0=R0_init*(heav(t)-heav(t-tonR0)) + R0_tar*heav(t-tonR0)




# IC
G(0)=99.7664
I(0)=9.025 
b(0)=2.00069  
gamma(0)=-0.006666 
sigma(0)=536.7163 
Si(0)=0.8 

 
G' = R0 - (Eg0 + Si*I)*G 
I' = b*ISR/BV - k*I
b' = (P-A)*b/tau_b 
gamma'=(gamma_inf - gamma)/tau_g
sigma'=(sigma_inf - sigma)/tau_sigma 
Si'=(heav(t)-heav(t-tonsi))*(-t_si*(Si - target_Si))

aux prol_r=P/tau_b
aux apop_r=A/tau_b
aux plot_M=M
aux plot_P=P
aux plot_A=A
aux plot_ISR=ISR
aux plot_Si=Si
aux plot_sig=sigma
aux plot_R0=R0

@ meth=cvode, toler=1.0e-10, atoler=1.0e-10, dt=0.053, total=200
@ maxstor=2000000,bounds=100000000000, xp=G, yp=sigma
@ xlo=0, xhi=300, ylo=-100, yhi=200
@ bell=0
aux thour=t*24
aux tmin=t*24*60
done
